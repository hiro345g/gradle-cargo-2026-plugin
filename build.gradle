plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '2.0.0'
    id 'org.ajoberstar.git-publish' version '3.0.0'
}

apply from: "$rootProject.projectDir/gradle/integration-test.gradle"

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withSourcesJar()
    withJavadocJar()
}
group = 'com.bmuschko'
version = '2.9.0'

repositories {
    mavenCentral()
}

dependencies {
    def cargoVersion = '1.9.10'
    implementation "org.codehaus.cargo:cargo-daemon-client:$cargoVersion"
    testImplementation('org.spockframework:spock-core:2.4-groovy-4.0')
    testImplementation "org.codehaus.cargo:cargo-ant:$cargoVersion"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.8.2'
}

task groovydocJar(type: Jar) {
    dependsOn groovydoc
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

gradlePlugin {
    website = "https://github.com/bmuschko/gradle-cargo-plugin"
    vcsUrl = "https://github.com/bmuschko/gradle-cargo-plugin"

    plugins {
        cargoBasePlugin {
            id = 'com.bmuschko.cargo-base'
            implementationClass = 'com.bmuschko.gradle.cargo.CargoBasePlugin'
            displayName = 'Gradle Cargo Base Plugin'
            description = 'Plugin that provides Cargo custom task types, pre-configures classpath and deployables.'
            tags.set(['deployment', 'container', 'base', 'cargo'])
        }
        cargoPlugin {
            id = 'com.bmuschko.cargo'
            implementationClass = 'com.bmuschko.gradle.cargo.CargoPlugin'
            displayName = 'Gradle Cargo Plugin'
            description = 'Plugin that provides a set of local and remote Cargo tasks and exposes extension for configuration.'
            tags.set(['deployment', 'container', 'web', 'ear', 'cargo'])
        }
    }
}

publishing {
    publications.withType(MavenPublication) {
        artifact groovydocJar
    }
}

gitPublish {
    repoUri = 'git@github.com:bmuschko/gradle-cargo-plugin.git'
    branch = 'gh-pages'

    contents {
        from(javadoc.outputs.files) {
            into 'docs/javadoc'
        }
        from(groovydoc.outputs.files) {
            into 'docs/groovydoc'
        }
    }
}

gitPublishCopy.dependsOn javadoc, groovydoc

test {
    useJUnitPlatform()
}