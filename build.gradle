plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '2.0.0'
    id 'org.ajoberstar.git-publish' version '3.0.0'
}

apply from: "$rootProject.projectDir/gradle/integration-test.gradle"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}
group = 'io.github.hiro345g'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    def cargoVersion = '1.10.0'
    implementation "org.codehaus.cargo:cargo-daemon-client:$cargoVersion"
    implementation "org.codehaus.cargo:cargo-ant:$cargoVersion"
    testImplementation('org.spockframework:spock-core:2.4-groovy-4.0')
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.8.2'
}

task groovydocJar(type: Jar) {
    dependsOn groovydoc
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

gradlePlugin {
    website = "https://github.com/hiro345g/gradle-cargo-2026-plugin"
    vcsUrl = "https://github.com/hiro345g/gradle-cargo-2026-plugin"

    plugins {
        cargoBasePlugin {
            id = 'io.github.hiro345g.cargo-2026-base'
            implementationClass = 'com.bmuschko.gradle.cargo.CargoBasePlugin'
            displayName = 'Gradle Cargo 2026 Base Plugin'
            description = 'Plugin that provides Cargo custom task types, pre-configures classpath and deployables.'
            tags.set(['deployment', 'container', 'base', 'cargo'])
        }
        cargoPlugin {
            id = 'io.github.hiro345g.cargo-2026'
            implementationClass = 'com.bmuschko.gradle.cargo.CargoPlugin'
            displayName = 'Gradle Cargo 2026 Plugin'
            description = 'Plugin that provides a set of local and remote Cargo tasks and exposes extension for configuration.'
            tags.set(['deployment', 'container', 'web', 'ear', 'cargo'])
        }
    }
}

publishing {
    publications.withType(MavenPublication) {
        artifact groovydocJar
        pom {
            name = 'Gradle Cargo 2026 Plugin'
            description = 'Fork of the original gradle-cargo-plugin updated for 2026.'
            url = 'https://github.com/hiro345g/gradle-cargo-2026-plugin'
            licenses {
                license {
                    name = 'The Apache Software License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
    }
}

gitPublish {
    repoUri = 'git@github.com:hiro345g/gradle-cargo-2026-plugin.git'
    branch = 'gh-pages'

    contents {
        from(javadoc.outputs.files) {
            into 'docs/javadoc'
        }
        from(groovydoc.outputs.files) {
            into 'docs/groovydoc'
        }
    }
}

gitPublishCopy.dependsOn javadoc, groovydoc

test {
    useJUnitPlatform()
}